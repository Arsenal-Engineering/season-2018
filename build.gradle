import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

// Applying plugins to Gradle
plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.01.06"
    id 'org.jetbrains.kotlin.jvm' version "1.2.10"
}

// Definitions
def TEAM = 6223
def ROBOT_CLASS = "frc.team6223.robot.Robot"

// Define the script targets (RoboRIO) and artifacts (deployable files)
deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcKotlin', FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

// Dependency Repositories
repositories {
    mavenCentral()
}

// Defining dependencies
// Robot: WPILib, CTRE Phoenix (Talon SRX), NavX,
// Kotlin: Kotlin StdLibrary, Kotlin Reflect
// Testing: KotlinTest
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile "org.jetbrains.kotlin:kotlin-stdlib"

    testCompile group: 'io.kotlintest', name: 'kotlintest', version: '2.0.7'
}

// Setting up jar file: generating fat jar, and adding WPILib manifest settings
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

// Setting a task to generate the wrapper files
task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}